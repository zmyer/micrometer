plugins {
    id "net.ltgt.apt" version "0.10" // for dagger injection testing
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'nebula.optional-base'

dependencies {
    compile 'org.pcollections:pcollections:latest.release'

    // JSR-305 only used for non-required meta-annotations
    compile "com.google.code.findbugs:jsr305:latest.release", optional

    compile 'org.hdrhistogram:HdrHistogram:latest.release'
    compile('org.latencyutils:LatencyUtils:latest.release') {
        exclude group: 'org.hdrhistogram', module: 'HdrHistogram'
    }

    // instrumentation options
    compile 'io.dropwizard.metrics:metrics-core:3.+', optional

    // cache monitoring
    compile 'com.google.guava:guava:latest.release', optional
    compile 'com.github.ben-manes.caffeine:caffeine:latest.release', optional
    compile 'net.sf.ehcache:ehcache:2.10.4', optional
    compile 'javax.cache:cache-api:1.0.0', optional
    compile 'com.hazelcast:hazelcast:3.8.+', optional
    compile 'org.hibernate:hibernate-entitymanager:latest.release', optional

    // server runtime monitoring
    compile 'org.eclipse.jetty:jetty-server:latest.release', optional
    compile 'org.apache.tomcat.embed:tomcat-embed-core:8.+', optional

    // hystrix monitoring
    compile 'com.netflix.hystrix:hystrix-core:latest.release', optional

    // log monitoring
    compile 'ch.qos.logback:logback-classic:1.2.+', optional

    // reactor
    compile 'io.projectreactor:reactor-core:latest.release', optional

    // @Timed AOP
    compile 'org.aspectj:aspectjweaver:1.8.+', optional

    compile 'com.squareup.okhttp3:okhttp:latest.release', optional

    testCompile 'io.projectreactor:reactor-test:latest.release'
    testCompile 'io.projectreactor.ipc:reactor-netty:latest.release'

    // JUnit 5
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.+'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.+'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.+'

    // Eclipse still needs this (as of 4.7.1a)
    testRuntime 'org.junit.platform:junit-platform-launcher:1.+'

    testCompile 'org.mockito:mockito-core:latest.release'

    testCompile 'org.hsqldb:hsqldb:latest.release'

    // dependency injection tests
    testCompile 'javax.inject:javax.inject:1'
    testCompile 'org.springframework:spring-context:4.+'
    testCompile 'org.springframework:spring-test:4.+'
    testCompile 'com.google.inject:guice:4.1.0'

    // Uncomment these if you are interested in testing injection with dagger in MeterRegistryInjectionTest
//    testCompile 'com.google.dagger:dagger:2.11'
//    testCompile 'com.google.dagger:dagger-compiler:2.11'

    testCompile 'org.assertj:assertj-core:latest.release'

    testCompile 'org.ehcache:ehcache:3.+'
}

shadowJar {
    configurations = [project.configurations.compile]
    classifier = null
    dependencies {
        include(dependency('org.pcollections:'))
    }
    relocate 'org.pcollections', 'io.micrometer.shaded.org.pcollections'
}

jar.deleteAllActions()
jar.dependsOn shadowJar

publishing {
    publications {
        withType(MavenPublication) {
            pom.withXml {
                asNode()
                        .dependencies
                        .dependency
                        .findAll {
                    ['pcollections'].contains(it.artifactId.text())
                }
                .each { it.parent().remove(it) }
            }
        }
    }
}